#!/bin/csh

set command = "time ./grid_multipolesAVX -box 2000 -scale 1 -rmax 90 -ngrid 40"
echo command
echo "Did you re-compile grid_multipoles for the correct NBIN?"

set tmp = /mnt/ssd/ted
set in = LasDamas
set out = LasDamas.v01
set errlog = $out/errlog
set ranroot = ldam

mkdir $out
rm -i $errlog

#set root = ldam.01a_no.real
foreach root ( \
    ldam.01a_no.real \
    ldam.01b_no.real \
    ldam.01c_no.real \
    ldam.01d_no.real \
    ldam.02a_no.real \
    ldam.02b_no.real \
    ldam.02c_no.real \
    ldam.02d_no.real \
)


    set multfile = $tmp/$root.mult

    # Get the data and run it, with storage
    gunzip -c $in/$root.dat.gz > $tmp/$root.dat

    echo "Starting DDD" >> $errlog
    date >> $errlog
    ($command -in $tmp/$root.dat -save $multfile \
	    > $out/$root.ddd.out) >&! $errlog

    echo Done with DDD


    # Get a base random and run it, with inversion
    gunzip -c $in/$ranroot.ran.00.gz > $tmp/$root.ran.0

    echo "Starting RRR" >> $errlog
    date >> $errlog
    ($command -in $tmp/$root.ran.0 -invert \
	    > $out/$root.rrr.out) >>& $errlog

    echo Done with RRR

    cat $tmp/$root.dat >> $tmp/$root.ran.0

    echo "Starting D-R check" >> $errlog
    date >> $errlog
    ($command -in $tmp/$root.ran.0 \
	    > $out/$root.ch0.out) >>& $errlog

    rm $tmp/$root.ran.0
    echo Done with D-R 0 check

    # Now make D-R and run that, with loading
    foreach n ( 00 01 02 03 04 05 06 07 08 09 )
	cp -f $tmp/$root.dat $tmp/$root.ran.$n
	gunzip -c $in/$ranroot.ran.$n.gz >> $tmp/$root.ran.$n

	echo "Starting D-R $n" >> $errlog
	date >> $errlog
	($command -in $tmp/$root.ran.$n -load $multfile \
		> $out/$root.r$n.out) >>& $errlog

	rm $tmp/$root.ran.$n
	echo Done with D-R $n

    end    # foreach D-R loop

end    # foreach root loop
